plugins {
    id "nu.studer.jooq"
    id "com.github.johnrengelman.shadow"
}

dependencies {
    // SQL driver and Flyway for data migration
    implementation("org.postgresql:postgresql:42.2.23")
    implementation("org.flywaydb:flyway-core:7.14.0")

    // jOOQ dependency itself
    implementation("org.jooq:jooq:3.15.2")

    // Dependencies during jOOQ compilation time during Gradle compilation phase
    jooqGenerator("org.postgresql:postgresql:42.2.23")
    jooqGenerator project(":generator")

    // Test container dependencies to start a PostgreSQL container
//    testImplementation("org.testcontainers:postgresql:1.16.0")
//    testImplementation("org.testcontainers:postgresql:1.16.0")
}

// Add the jOOQ plugin to generate the jOOQ DSL code
jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                generator {
                    database {
                        // Here use our custom database testcontainer
                        name = "ch.swaechter.jooqexample.generator.StandalonePostgreSqlDatabase"
                        inputSchema = "public"
                        includes = ".*"
                        excludes = ""
                    }
                    generate {
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = "ch.swaechter.jooqexample.api"
                    }
                }
            }
        }
    }
}

// Make the generateJooq task dependent on the input files so we trigger a clean rebuild when the files change
tasks.named("generateJooq").configure {
    // Declare the migration scripts as input for the jOOQ task (This allows proper build caching)
    inputs.files(fileTree("src/main/resources/db/migration")).withPropertyName("migrations").withPathSensitivity(PathSensitivity.RELATIVE)

    // Let the task participate in incremental builds and build caching
    allInputsDeclared = true
    outputs.cacheIf {
        true
    }
}

// Define a fat JAR so we can see what's inside a packaged application
shadowJar {
    manifest {
        attributes "Main-Class": "ch.swaechter.jooqexample.application.Application"
    }
}

// Make the assembly ("package") task depend on the shadow/fat JAR task so a fat JAR is generated
assemble.dependsOn(shadowJar)
