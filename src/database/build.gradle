plugins {
    id "nu.studer.jooq"
}

dependencies {
    // Dependencies during jOOQ compilation time during Gradle compilation phase
    jooqGenerator("org.postgresql:postgresql:42.2.18")
    jooqGenerator project(":generator")

    // Test container dependencies to start a PostgreSQL container
    testImplementation "org.testcontainers:postgresql:1.15.1"
    testImplementation("org.testcontainers:postgresql:1.15.1")
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                generator {
                    database {
                        name = "ch.swaechter.jooqexample.generator.StandalonePostgreSqlDatabase"
                        inputSchema = "public"
                        includes = ".*"
                        excludes = ""
                    }
                    generate {
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = "ch.swaechter.jooqexample.api"
                    }
                }
            }
        }
    }
}

tasks.named("generateJooq").configure {
    // Declare the migration scripts as input for the jOOQ task (This allows proper build caching)
    inputs.files(fileTree("src/main/resources/db/migration")).withPropertyName("migrations").withPathSensitivity(PathSensitivity.RELATIVE)

    // Let the task participate in incremental builds and build caching
    allInputsDeclared = true
    outputs.cacheIf {
        true
    }
}
